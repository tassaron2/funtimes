version = 1
# example predicament to show all funtimes features
# most syntax is simple: keyword = value

predicament = start
    # this is the default startpoint for a game
    type = goto -> entry
    set entrymsg = Entering through funplayer.py breaks the game!
/predicament this line ends a predicament definition

predicament = entry
    # % is used to insert variables in text
    # the variable will print as nothing if it doesn't exist

    map =
    >>>>>>>>>>
    ^        ^
    ^        ^
    ^        ^
    ^        ^
    ^        ^
             ^
             ^
    ^        ^
    ^>>>>>>>>>
    /map
    dude = Y -> you
    dude = A -> girl
    # dudes can be placed at an x,y position on the map
    A = 6,4
    Y = 4,6
    
    name = Welcome to FUNTIMES!
    text = %entrymsg%
    set number = random
    set progress = random
    text = Two random numbers: %number% %progress%
    set progress = %number%
    subtract 200 from number
    text = %progress% - 200 = %number%
    set entrymsg = Stop waiting around! :p

    function = generateGirlName

    entry = %girlname% was generated by if statements
    set talkedtogirl = 0
    action = OK -> walkaround1
    
/predicament anything else on this line isn't read, like a comment

predicament = basicstuff

    action = OK -> walkaround1
/predicament

predicament = walkaround1
    name = The First Room
    map =
    >>>>>>>>>>
    ^        ^
    ^        ^
    ^        ^
    ^        ^
    ^        ^
             ^
             ^
    ^        ^
    ^>>>>>>>>>
    /map
    if followed is 1
        then
        dude = G -> follower
    /if
    entry = A non-descript room with a dude in it.
    dude = A -> talker
    dude = Y -> you
    A = 6,4
    Y = 4,6
    if talkedtogirl is 1
        then
        left = walkaround2
    /if
    function = badfollow
/predicament

predicament = walkaround2
    # to draw this map correctly (with floor tiles)
    # had to disable 'strip trailing spaces'
    name = Hub Room
    map =
    >>  >>>>>>
    ^        ^
    ^        ^
    ^        ^
    ^        ^
    ^  Y     ^
    ^         
    ^         
    ^        ^
    ^>  >>>>>>
    /map
    dude = Y -> you
    Y = 400,400
    #  ^-- this line gets ignored because
    # the position of this dude is defined on the map
    
    if followed is 0
        then
        text = More options for movement, but where do they go?
    /if
    if followed is 1
        then
        dude = G -> follower
    /if
    
    right = walkaround1
    up = walkaround2N
    if badguyQ is not done
        then
        down = walkaround2S
    /if
    if badguyQ is done
        then
        if heardthatnoise is 0
            then
            text = Heard a weird sound from the south...
            set heardthatnoise = 1
        /if
        down = southbranch-entry
    /if
    function = badfollow
/predicament

predicament = walkaround2S
    up = walkaround2
    map =
        >>  >>>>>>
        ^        ^
        ^    Y   ^
        ^        ^
        ^   >>>>>^
        ^        ^
        ^        ^
        ^        ^
        ^        ^
        ^>>>>>>>>>
    /map

    dude = Y -> you
    if badfollow is 0
        then
        entry = Nothing interesting here...
    /if
    if badguyQ is 1
        then
        text = At least that freaking probe isn't here!
    /if
    function = badfollow
/predicament

# If you comment out this predicament, it will
# not be inserted on every screen as a 'Quit' button
predicament = quit
    quit
/predicament

predicament = kill
    quit
/predicament

predicament = generateGirlName
    # now let's use some if statements!!!
    if progress is < 41
        then <-- must be on its own line
        set girlname = Megan
    /if  <-- this line ends the if statement
    if progress is > 40
        and
    if progress is < 61
        then
        set girlname = Brianna
    /if
    if progress is > 60
        and
    if progress is < 81
        then
        set girlname = Anna
    /if
    if progress is > 80
        then
        set girlname = Laurie
    /if
/predicament

predicament = badfollow
    if badfollow is 1
        then
        dude = J -> badguy
        J = 4, 4
    /if
/predicament
