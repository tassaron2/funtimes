version = 1
# dudes are tick lovers

dude = girl
    tile = ~/girl.png
/dude

dude = talker
    name = %girlname%
    tile = ~/girl.png
    tick = 1
        text = Hi, I'm %girlname%! Press 'Wait' to see more of what I have to say.
        set talkedtogirl = 1
    /tick
    tick = 2
        text = In Funtimes, events only happen when you make an action. The Wait button allows you to watch time passing without doing anything.
        text = Now you can go left and explore the rest of this area.
    /tick
    tick = 3
        text = All of my actions and text are defined in 'example.dude', which is a plaintext file.
        text = Making your own game with Funtimes is... relatively simple!
    /tick
    tick = 4
        text = This is the 4th tick since you entered the room. I'm going to start counting from 300.
        set progress = 300
        set number = 299
    /tick
    # EVERY TICK
    if progress is > 299
        then
        add 1 to progress
        add 1 to number
        text = %number%, %progress%...
    /if
/dude

dude = badguy
    tile = ~/probe.png
    if %badguyQ% = 0
        then
        text = A probe queries you for information.
        set badguyQ = 1
    /if
    
    if %badguyQ% is 1
        then
        text = The probe insists that you identify yourself.
    /if
    
    if %badguyQ% is 2
        then
        text = It's not convinced!
        set badfollow = 1
        set badguyQ = 3
        set badguyQlabel = Attack the probe
        set probeAttackMsg = The probe attacks!!!
    /if
    
    if %badguyQ% is > 2
        and
    if badguyQ is < 6
        then
        text = %probeAttackMsg%
    /if
    
    if badguyQ is 4
        then
        text = You attack back!
        set badguyQ = 5
    /if
    
    # percent signs are mandatory on the right side
    if badguyQ is %finalQ%
        then
        text = The probe is deactivated!
        set badfollow to 0
        # without % this sets the variable to a string
        set badguyQ = done
        set badguyQlabel = OK
        # once it becomes a string it can't become a num again
    /if

    if badguyQ is < %finalQ%
        then
        action = %badguyQlabel% -> badguyQpred
    /if
/dude


dude = follower
    tile = ~/follower.png
    if followed is 0
        then
        set followed = 1
        set follownum = 0
    /if 
    if followed is 1
        then
        add 1 to follownum
        text = Follower is watching you.
    /if
    if follownum is > 5
        then
        action = Punch that stupid follower! -> punchfollower
    /if
/dude

dude = key
    tile = ~/key.png
    text = It left behind a key! But where is the door?
    set nomorekey=1
/dude

dude = axemurderer
    tile = ~/axemurderer.png
    tick = 2
        if safety is not 1
            then
            set dead = 1
            text = YOU'RE DEAD
        /if
    /tick
/dude

dude = axemurdererprop
    tile = ~/axemurderer.png
/dude

dude = you
    tile = ~/you.png
/dude
